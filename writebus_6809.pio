/**
 * Copyright (c) 2021 Chris Moulang
 *
 * SPDX-License-Identifier: BSD-3-Clause
*/

/* 
    Modified for 6809, quadrature clock.
    Q leads E by 90deg
    Address lines are valid after rising edge of Q.
    Data is strobed out (from the CPU), on falling edge of Q.
    Data is strobed in on falling edge of E.
*/

.program writebus

.define public  PIN_RW      0
.define public  PIN_E       1
.define public  PIN_A0      2
.define public  PIN_SEL1    26

.define NOTHING             0b111
.define ADDRESS_LOW         0b011
.define ADDRESS_HIGH        0b101
.define DATA                0b110

//.define tSELECT 3   ; = 20nS at 150MHz
.side_set 3 opt
    wait 0 GPIO PIN_E                                   ; ensure SM starts at beginning of a bus cycle

loop:
    wait 1 GPIO PIN_E       side ADDRESS_LOW            ; wait for E clock 0 -> 1
    nop [1]
    nop [1]
    in PINS 8                                           ; read address 0-7
    jmp PIN handle_read     side ADDRESS_HIGH           ; test for read and select address 8-15
    nop [1]
    nop [1]                  
    set y 1 [1]
    in PINS 8                                           ; read address 8-15

    wait 0 GPIO PIN_E       side DATA                   ; wait for E clock 1 -> 0
    in PINS 8                                           ; read data
    in y 8                                              ; 1 indicates 6809 write
                                                        ; Will autopush here
    jmp loop

handle_read:
    nop [1]                                             ; delay until 74lvc245 is ready
    nop [1]                  
    nop [1]
    in PINS 8                                           ; read address 8-15
    in NULL 16                                          ; pad with 0's
                                                        ; Will autopush here

    wait 0 GPIO PIN_E                                   ; wait for clock E 1 -> 0

    jmp loop

% c-sdk {
// this is a raw helper function for use by the user which sets up the GPIO output, and configures the SM to output on a particular pin

void writebus_program_init(PIO pio, uint sm, uint offset) {
   pio_sm_config c = writebus_program_get_default_config(offset);
   sm_config_set_jmp_pin 	(&c, writebus_PIN_RW);
   sm_config_set_in_pins(&c, writebus_PIN_A0);
   sm_config_set_set_pins(&c, writebus_PIN_SEL1, 3);

   sm_config_set_sideset(&c, 4, true, false);
   sm_config_set_sideset_pins(&c, writebus_PIN_SEL1);

   sm_config_set_in_shift(&c, true, true, 32);
   pio_sm_set_consecutive_pindirs (pio, sm, writebus_PIN_SEL1, 3, true);
   
   sm_config_set_clkdiv(&c, 1);
   
   pio_sm_init(pio, sm, offset, &c);
}

%}
